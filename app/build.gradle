
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    compileSdk 35

    def _patch
    def versionPropsFile = file("version.properties")

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        _patch = versionProps['PATCH'].toInteger() + 1
        versionProps['PATCH'] = _patch.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new GradleException("Could not read version.properties!")
        //_patch = "E"
    }
    def _versionName = "3." + _patch

    defaultConfig {
        applicationId "ua.com.programmer.barcodetest"
        minSdkVersion 23
        targetSdkVersion 35
        versionCode _patch
        versionName _versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    dataBinding {
        enabled true
    }
    buildFeatures {
        buildConfig = true
    }
    namespace 'ua.com.programmer.barcodetest'
}

// Secrets Gradle Plugin configuration
// The plugin automatically reads from local.properties and exposes properties
// as BuildConfig fields. Properties are converted to uppercase.
// FIREBASE_USER and FIREBASE_USER_PASS from local.properties will be available
// as BuildConfig.FIREBASE_USER and BuildConfig.FIREBASE_USER_PASS

dependencies {
    implementation 'com.google.guava:guava:32.1.3-android'

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // CameraX dependencies
    def camera_version = "1.4.1"
    implementation "androidx.camera:camera-camera2:$camera_version"
    implementation "androidx.camera:camera-lifecycle:$camera_version"
    implementation "androidx.camera:camera-view:$camera_version"

    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.0'
    //implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:33.9.0')
    //implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-auth'

    // Hilt
    implementation "com.google.dagger:hilt-android:2.54"
    kapt "com.google.dagger:hilt-compiler:2.54"

    testImplementation 'junit:junit:4.13.2'
}

// Hilt
// Allow references to generated code
kapt {
    correctErrorTypes true
}